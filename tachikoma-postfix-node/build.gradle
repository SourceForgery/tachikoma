apply plugin: 'com.moowork.node'
apply plugin: 'de.richsource.gradle.plugins.typescript'
apply plugin: 'base'

import com.moowork.gradle.node.NodeExtension
import com.moowork.gradle.node.variant.VariantBuilder

node {
    download = true
    version = '9.4.0'
    npmVersion = '5.6.0'
}

def protobufDir = file("$buildDir/proto/")
def typingsDir = file("typings/")

configurations {
    protobuf
}

String nodeExecutable() {
    NodeExtension nodeExt = NodeExtension.get(project)
    return new VariantBuilder(nodeExt).build().nodeExec
}

compileTypeScript {
    projectFile = new File('./tsconfig.json')
    dependsOn "npmInstall"
}

dependencies {
    protobuf project(':tachikoma-backend-api-proto')
}


idea {
    module {
        sourceDirs += protobufDir
    }
}


task copyProtobufFiles([type: Sync]) {
    configurations.protobuf.each {
        from(zipTree(it)) {
            include '**/*.proto'
            exclude 'META-INF/**'
        }
    }
    includeEmptyDirs = false
    into protobufDir
}

task buildProtoJs([type: NodeTask]) {
    script = file('./node_modules/protobufjs/cli/bin/pbjs')
    def arguments = [
            '-t', 'static-module',
            '-w', 'commonjs',
            '-o', "$protobufDir/mtagrpc.js",
            '-p', protobufDir,
            '--es6',
            '--force-long'
    ]
    args = arguments
    fileTree(protobufDir)
            .include('**/*.proto')
            .collect()
            .sort()
            .each { addArgs(it) }
}

task buildProtoTs(type: NodeTask) {
    script = file('./node_modules/protobufjs/cli/bin/pbts')
    args = [
            '--out', "$typingsDir/mtagrpc.d.ts",
            "$protobufDir/mtagrpc.js",
    ]
}



assemble.dependsOn compileTypeScript
buildProtoJs.dependsOn copyProtobufFiles
buildProtoTs.dependsOn buildProtoJs
compileTypeScript.dependsOn buildProtoTs